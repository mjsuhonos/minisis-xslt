<?xml version="1.0" encoding="UTF-8" ?>
 
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
 
	<xsl:output encoding="UTF-8" 
				indent="yes" 
				method="xml" 
				version="1.0"
				media-type="text/xml"/>

	<xsl:preserve-space elements="*"/>

	<!--
		Root node (typically <record_set/>)
	-->
	<xsl:template match="/">
		<!-- start at every top-level record -->
		<xsl:apply-templates select="//XML_RECORD[TOP_LEVEL_FLAG='Y']" mode="toplevel"/>
	</xsl:template>

	<!--
		EAD Wrapper
		http://www.loc.gov/ead/tglib/elements/ead.html
	-->
	<xsl:template match="XML_RECORD" mode="toplevel">
		<ead>

			<!-- TODO: MAPPING TO REPLACE SAMPLE CONTENT -->
			<eadheader langencoding="iso639-2b" countryencoding="iso3166-1" dateencoding="iso8601" repositoryencoding="iso15511" scriptencoding="iso15924">
				<eadid countrycode="CA" mainagencycode="CA-ON00009" encodinganalog="Identifier"><xsl:value-of select="REFD"/></eadid>
			    <filedesc>
			        <titlestmt>
			            <titleproper><xsl:value-of select="TITLE"/></titleproper>
			        </titlestmt>
			    </filedesc>
			    <profiledesc>
			        <creation>Generated by minisis-xslt <!-- TODO: set date manually for transformation --> <date normal="2013-07-08">2013-07-08 19:52:UTC</date>
					</creation>
					<langusage>
						<language langcode="eng" encodinganalog="Language">English</language>
					</langusage>
			    </profiledesc>
			</eadheader>

			<frontmatter/> 	<!-- optional -->

			<archdesc level="{LEVEL_DESC}">
				<xsl:call-template name="description"/>
			</archdesc>
		</ead>
	</xsl:template>

	<!--
		Component wrapper (unnumbered)
		http://www.loc.gov/ead/tglib/elements/c.html
	-->
	<xsl:template match="XML_RECORD">
		<c level="{LEVEL_DESC}">
			<xsl:call-template name="description"/>
		</c>
	</xsl:template>

	<!--
		Template for archdesc/c
		http://www.loc.gov/ead/tglib/appendix_c.html
	-->
	<xsl:template name="description">
		<!-- May contain: accessrestrict, accruals, acqinfo, altformavail, appraisal, arrangement, bibliography, bioghist, controlaccess, custodhist, dao, daogrp, descgrp, did, dsc, fileplan, index, note, odd, originalsloc, otherfindaid, phystech, prefercite, processinfo, relatedmaterial, runner, scopecontent, separatedmaterial, userestrict -->

		<!-- TODO: MAPPING TO REPLACE SAMPLE CONTENT -->
		<did>
			<head>Descriptive Summary</head>
			<unittitle label="Title"><xsl:value-of select="TITLE"/></unittitle>
			<unitdate type="inclusive" normal="{DATE_SEARCH}"><xsl:value-of select="DATE_CR_INC"/></unitdate>
			<unitid countrycode="CA" repositorycode="cu-i" label="Collection number"><xsl:value-of select="REFD"/></unitid>
			<xsl:for-each select="ORIGINATION_GRP">
				<origination label="{EAD_LABEL}">
					<xsl:element name="{EAD_ROLE}"><xsl:value-of select="ORIGINATOR"/></xsl:element>
				</origination>
			</xsl:for-each>
			<physdesc>
				<xsl:value-of select="PHYSICAL_DESC"/>
				<physfacet><xsl:value-of select="PHYSICAL_COND"/></physfacet>
			</physdesc>
			<repository label="Repository">
				<corpname>Ryerson University Library and Archives</corpname>
				<address>
					<addressline>350 Victoria Street</addressline>
					<addressline>2nd Floor</addressline>
					<addressline>Toronto</addressline>
					<addressline>Ontario</addressline>
					<addressline>Canada</addressline>
					<addressline>M5B 2K3</addressline>
					<addressline>Telephone: 416-979-5055</addressline>
					<addressline>Fax: 416-979-5215</addressline>
					<addressline>Email: archives@ryerson.ca</addressline>
					<addressline>http://library.ryerson.ca</addressline>
				</address>
			</repository>
			<!--
			<abstract label="Abstract"><xsl:value-of select="ABSTRACT"/></abstract>
			-->
		</did>
		<arrangement><xsl:value-of select="ARRANGEMENT"/></arrangement>
		<xsl:for-each select="NOTES">
			<note><xsl:value-of select="."/></note>
		</xsl:for-each>
		<descgrp>
			<head>Important Information for Users of the Collection</head>
			<accessrestrict><xsl:value-of select="RESTRICTIONS"/></accessrestrict>
			<userestrict>
				<head>Publication Rights</head>
				<p>Property rights reside with the University of California. Literary rights are
	            retained by the creators of the records and their heirs. For permissions to reproduce or
	            to publish, please contact the Head of Special Collections and Archives.</p>
			</userestrict>
			<prefercite>
				<head>Preferred Citation</head>
				<p>Mildred Davenport Dance Programs and Dance School Materials. MS-P29. Special
	            Collections and Archives, The UC Irvine Libraries, Irvine, California.</p>
			</prefercite>
			<acqinfo><xsl:value-of select="IMM_SOURCE_ACQ"/></acqinfo>
			<processinfo>
				<head>Processing History</head>
				<p>Processed by Adrian Turner, 2001.</p>
			</processinfo>
		</descgrp>
		<bioghist><xsl:value-of select="BIO_SKETCH"/></bioghist>
		<scopecontent><xsl:value-of select="SCOPE"/></scopecontent>
		<controlaccess>
			<head>Indexing Terms</head>
			<p>The following terms have been used to index the description of this collection
	            in the library's online public access catalog.</p>
			<controlaccess>
				<head>Subjects</head>
				<persname encodinganalog="600" rules="aacr2" role="subject">Davenport, Mildred,
	            1900-1990--Archives.</persname>
				<subject encodinganalog="650" source="lcsh">African American dancers--Archival
	            resources.</subject>
			</controlaccess>
			<controlaccess>
				<head>Occupations</head>
				<occupation encodinganalog="656" source="aat">Dancers.</occupation>
				<occupation encodinganalog="656" source="lcsh">Dance teachers.</occupation>
			</controlaccess>
		</controlaccess>
		
		<xsl:if test="boolean(REFD_LOWEREXIST)">
			<dsc type="combined">
				<!-- May contain: address, blockquote, c, c01, chronlist, dsc, head, list, note, p, table, thead -->

				<xsl:for-each select="LOWER_LEVEL">
					<xsl:variable name="lower_level_code" select="LOWER_CODE"/>

					<!-- recurse through the hierarchy -->
					<xsl:apply-templates select="//XML_RECORD[REFD=$lower_level_code]"/>
				</xsl:for-each>
			</dsc>
		</xsl:if>

	</xsl:template>
</xsl:stylesheet>